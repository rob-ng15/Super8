// MEMMAP
//  c000-       UART
//  c010-       KBD
//  c020-       TIMER
unit memory_io(
    input   uint8   MAIN_in_address,
    input   uint8   MAIN_in_data,
    output  uint8   MAIN_out_data,
    input   uint1   MAIN_req_read,
    input   uint1   MAIN_req_write,

    output  uint1   uart_tx,
    input   uint1   uart_rx,
    // USB for PS/2
    input   uint1   us2_bd_dp,
    input   uint1   us2_bd_dn,

    output  uint1   PULSE_1Hz,
    output  uint1   PULSE_100Hz,
) {
    ps2_IN PS2_IN( us2_bd_dp <: us2_bd_dp, us2_bd_dn <: us2_bd_dn );                                                            // PS2 EVENT QUEUE, DECODED MY MOS

    uart_IN UART_IN( uart_rx <: uart_rx );                                                                                      // UART INPUT QUEUE
    uart_OUT UART_OUT( uart_tx :> uart_tx, out_data <: MAIN_in_data );                                                          // UART OUTPUT QUEUE

    timer_pulse1hz CURSOR_BLINK( pulse1hz :> PULSE_1Hz );                                                                       // 1 SECOND PULSE, USED FOR CURSOR BLINK
    timer_pulse100hz CENTISECOND( pulse100hz :> PULSE_100Hz );                                                                  // 100 Hz PULSE, USED FOR TIMER INTERRUPTS

    UART_IN.req_read := 0; UART_IN.CLEAR := 0; UART_OUT.CLEAR := 0;
    PS2_IN.req_read := 0; PS2_IN.CLEAR := 0;

    algorithm <autorun> { while(1) {
        switch( MAIN_in_address ) {
            case 8h00: { MAIN_out_data = UART_OUT.out_full; }                                                                   // UART OUT STATUS
            case 8h01: { UART_OUT.req_write = MAIN_req_write; }                                                                 // UART OUT WRITE
            case 8h02: { MAIN_out_data = UART_IN.out_available; }                                                               // UART IN STATUS
            case 8h03: { MAIN_out_data = UART_IN.in_data; UART_IN.req_read = MAIN_req_read; }                                   // UART IN READ
            case 8h04: { UART_IN.CLEAR = MAIN_req_write; UART_OUT.CLEAR = MAIN_req_write; }                                     // UART RESET
            case 8h10: { MAIN_out_data = PS2_IN.key_available; }                                                                // PS2 IN STATUS
            case 8h11: { MAIN_out_data = PS2_IN.keycode; PS2_IN.req_read = MAIN_req_read; }                                     // PS2 IN READ
            case 8h12: { PS2_IN.CLEAR = MAIN_req_write; }                                                                       // PS2 RESET
            case 8h20: { MAIN_out_data = CENTISECOND.pulse100hz; }                                                              // TIMER PULSE?
            case 8h21: { if( MAIN_req_write ) { CENTISECOND.enable = MAIN_in_data; } }                                          // TIMER ENABLE INTERRUPTS
            case 8h22: { CENTISECOND.acknowledge = MAIN_req_read; }                                                             // TIMER ACKNOWLEDGE INTERRUPT
            default: {}
        }
    } }
}

// FIFO BUFFER FOR 8 BITS, FIRST IN, LAST OUT, HOLDS 256 ENTRIES
unit fifo8(
    input   uint1   CLEAR,
    output  uint1   out_available,
    output  uint1   in_full,
    input   uint1   req_read,
    input   uint1   req_write,
    output  uint8   out_data,
    input   uint8   in_data
) {
    simple_dualport_bram uint8 queue[256] = uninitialized;
    uint8   next_p1 <:: queue.addr0 + 1;
    uint8   top_p1 <:: queue.addr1 + 1;
    uint8   top_p2 <:: queue.addr1 + 2;

    queue.wenable1 := 0; queue.wdata1 := in_data; out_data := queue.rdata0;
    out_available := ( top_p1 != queue.addr0 ); in_full := ( top_p2 == queue.addr0 );                                           // CHARACTER AVAILABLE? QUEUE FULL?

    algorithm <autorun> {
        queue.addr0 = 0; queue.addr1 = -1;

        while(1) {
            queue.addr1 = queue.addr1 + req_write; queue.wenable1 = req_write;
            queue.addr0 = queue.addr0 + req_read;
            if( CLEAR ) { queue.addr0 = 0; queue.addr1 = -1; }                                                                  // CLEAR QUEUE
        }
    }
}

unit uart_IN(
    // UART
    input   uint1   CLEAR,
    input   uint1   uart_rx,
    output  uint1   out_available,
    output  uint8   in_data,
    input   uint1   req_read
) {
    uart_in ui; uart_receiver urecv( io <:> ui, uart_rx <: uart_rx );                                                           // CONNECT TO UART RECEIVE PINS
    fifo8 IN(                                                                                                                   // UART IN FIFO BUFFER
        CLEAR <: CLEAR,
        out_available :> out_available,
        out_data :> in_data,
        req_read <: req_read,
        in_data <: ui.data_out,
        req_write <: ui.data_out_ready
    );
}
unit uart_OUT(
    // UART
    input   uint1   CLEAR,
    output  uint1   uart_tx,
    output  uint1   out_full,
    input   uint8   out_data,
    input   uint1   req_write
) {
    uart_out uo; uart_sender usend( io <:> uo, uart_tx :> uart_tx );                                                            // CONNECT TO UART TRANSMIT PINS
    fifo8 OUT(                                                                                                                  // UART OUT FIFO BUFFER
        CLEAR <: CLEAR,
        in_full :> out_full,
        in_data <: out_data,
        req_write <: req_write,
        out_data :> uo.data_in
    );
    OUT.req_read := OUT.out_available & ~uo.busy; uo.data_in_ready := OUT.out_available & ~uo.busy;                             // SEND TO UART IF CHARACTER AVAILABLE AND UART NOT BUSY
}

unit ps2_IN(
    input   uint1   CLEAR,
    input   uint1   us2_bd_dp,
    input   uint1   us2_bd_dn,
    output  uint1   key_available,
    output  uint8   keycode,
    input   uint1   req_read
) {
    ps2 PS2( ps2clk_ext <: us2_bd_dp, ps2data_ext <: us2_bd_dn );
    fifo8 IN(                                                                                                                   // PS2 IN FIFO BUFFER
        CLEAR <: CLEAR,
        out_available :> key_available,
        in_data <: PS2.data,
        req_read <: req_read,
        out_data :> keycode,
        req_write <: PS2.valid
    );
}

// Create 1hz (1 second counter)
unit timer_pulse1hz(
    output  uint1   pulse1hz,
    input   uint1   resetCounter
) {
    algorithm <autorun> {
        uint26  counter50mhz = uninitialised;
        uint1   zero <:: ~|counter50mhz;
        uint26  counter50mhz_m1 <:: counter50mhz -1 ;

        while(1) {
            if( resetCounter ) {
                pulse1hz = 0; counter50mhz = 50000000;
            } else {
                if( zero ) { pulse1hz = ~pulse1hz; counter50mhz = 50000000; } else { counter50mhz = counter50mhz_m1; }
            }
        }
    }
}

// Create 100hz ( 100 times per second pulse )
unit timer_pulse100hz(
    input   uint1   enable,
    input   uint1   acknowledge,
    output  uint1   pulse100hz,
) {
    algorithm <autorun> {
        uint18  counter50mhz = uninitialised;
        uint1   zero <:: ~|counter50mhz;
        uint18  counter50mhz_m1 <:: counter50mhz -1 ;

        while(1) {
            if( enable ) {
                if( zero ) { pulse100hz = 1; counter50mhz = 500000; } else { counter50mhz = counter50mhz_m1; }
            } else {
                counter50mhz = 500000;
            }
            if( acknowledge ) { pulse100hz = 0; }
        }
    }
}
